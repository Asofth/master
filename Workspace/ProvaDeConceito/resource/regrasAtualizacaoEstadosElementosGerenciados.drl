import controle.evento.*;
import controle.dominio.*;
import controle.agente.atuador.*

declare EventoNumeroMensagensFila
	@role( event )
	@expires( 1s )
end

declare EventoInstanciaAtiva
	@role( event )
	@expires( 1s )
end

declare EventoInstanciaInativa
	@role( event )
	@expires( 1s )
end

rule "estadoAtivoFilaMensagens"
	activation-group "atualizacao-fila"
	no-loop true
when 
	$evento : EventoNumeroMensagensFila(numeroMensagensFila <= 100)
	from entry-point "FluxoDeEventos"
	$elemento : Atributo(identificador == IdentificadorAtributoElementoGerenciado.NUMERO_MENSAGENS 
							&& programa.identificador == IdentificadorElementoGerenciado.FILA_MENSAGEM);
then
	modify ($elemento) {
		setEstadoAtual(Estado.NORMAL);
	}
	System.out.println("FILA - NORMAL! " + $elemento.toString());	
end

rule "estadoAlertaFilaMensagens"
	activation-group "atualizacao-fila"
	no-loop true
when 
	$evento : EventoNumeroMensagensFila(numeroMensagensFila > 100 && numeroMensagensFila <= 200)
	from entry-point "FluxoDeEventos"
	$elemento : Atributo(identificador == IdentificadorAtributoElementoGerenciado.NUMERO_MENSAGENS 
							&& programa.identificador == IdentificadorElementoGerenciado.FILA_MENSAGEM);
then
	modify ($elemento) {
		setEstadoAtual(Estado.ALERTA);
	}
	System.out.println("FILA - ALERTA! " + $elemento.toString());	
end

rule "estadoCriticoFilaMensagens"
	activation-group "atualizacao-fila"
	no-loop true
when 
	$evento : EventoNumeroMensagensFila(numeroMensagensFila > 200)
	from entry-point "FluxoDeEventos"
	$elemento : Atributo(identificador == IdentificadorAtributoElementoGerenciado.NUMERO_MENSAGENS 
							&& programa.identificador == IdentificadorElementoGerenciado.FILA_MENSAGEM);
then
	modify ($elemento) {
		setEstadoAtual(Estado.CRITICO);
	}
	System.out.println("FILA - CRITICO! " + $elemento.toString());	
end

rule "estadoExecutorAtivo"
	activation-group "executor"
	no-loop true
when 
	$evento : EventoInstanciaAtiva(identificadorElementoGerenciado != null)
	from entry-point "FluxoDeEventos"
	$elemento : Atributo(identificador == $evento.identificadorAtributoElementoGerenciado 
							&& programa.identificador == $evento.getIdentificadorElementoGerenciado() );
then
	modify ($elemento) {
		setEstadoAtual(Estado.NORMAL);
	}
	System.out.println("EXECUTOR=" + $elemento.toString());	
end

rule "estadoExecutorInativo"
	activation-group "executor"
	no-loop true
when 
	$evento : EventoInstanciaInativa(identificadorElementoGerenciado != null)
	from entry-point "FluxoDeEventos"
	$elemento : Atributo(identificador == $evento.identificadorAtributoElementoGerenciado 
							&& programa.identificador == $evento.getIdentificadorElementoGerenciado() );
then
	modify ($elemento) {
		setEstadoAtual(Estado.INATIVO);
	}
	System.out.println("EXECUTOR=" + $elemento.toString());	
end
